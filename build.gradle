plugins {
    id "java"

    // generates coverage data
    // see homepage: http://eclemma.org/jacoco/
    // see plugin: https://docs.gradle.org/current/userguide/jacoco_plugin.html
    id "jacoco"

    // running mutating coverage tests
    // see homepage: http://pitest.org/
    // see plugin: http://gradle-pitest-plugin.solidsoft.info/
    id "info.solidsoft.pitest" version "1.1.4"

    // used to publish the coverage data to coveralls
    // see homepage: https://coveralls.io/
    // see plugin: https://github.com/kt3k/coveralls-gradle-plugin/
    id "com.github.kt3k.coveralls" version "2.4.0"

    // tracks dependency status
    // see homepage: https://www.versioneye.com/
    // see plugin: https://github.com/stempler/gradle-versioneye-plugin
    id "org.standardout.versioneye" version "1.0.1"

    // upload to bintray / jcenter
    // see homepage: https://bintray.com
    // see plugin: https://github.com/bintray/gradle-bintray-plugin
    id "com.jfrog.bintray" version "1.3.1"
}

sourceCompatibility = 1.8
version = '1.0'

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

pitest {
    pitestVersion = '1.1.7'
    targetClasses = ['de.thatsich.*']
    threads = 4
    outputFormats = ['HTML', 'XML']
    mutators = ['ALL']
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'de.thatsich'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/thatsIch/PITExample.git'
        version {
            name = '0.1.0'
            desc = 'Project with various plugins'
            released = new Date()
            vcsTag = '0.1.0'
            attributes = ['testattribute': 'some attribute']
        }
    }
    configurations = ['archives']
}